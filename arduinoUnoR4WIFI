#include <Servo.h>
#include <DHT.h>
#include <Wire.h>
#include <Adafruit_TSL2591.h>

const int trigPin = 2;      // HC-SR04 TRIG
const int echoPin = 3;      // HC-SR04 ECHO  
const int ledPin = 13;      // LED
const int servoPin = 9;     // SERVO
const int dhtPin = 4;       // DHT22 DATA
const int tslIntPin = 5;    // TSL2591 INT

Servo myServo;
DHT dht(dhtPin, DHT22);
Adafruit_TSL2591 tsl = Adafruit_TSL2591(2591);

int currentServoAngle = 90;
bool ledState = false;
bool blinking = false;
unsigned long previousBlinkMillis = 0;
unsigned long blinkInterval = 500;

void setup() {
  Serial.begin(9600);
  
  pinMode(ledPin, OUTPUT);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(tslIntPin, INPUT_PULLUP);
  
  dht.begin();
  
  if (tsl.begin()) {
    Serial.println("TSL2591 found!");
    tsl.setGain(TSL2591_GAIN_MED);
    tsl.setTiming(TSL2591_INTEGRATIONTIME_300MS);
  } else {
    Serial.println("TSL2591 not found!");
  }
  
  myServo.attach(servoPin, 544, 2400);
  myServo.write(currentServoAngle);
  delay(1000);
  myServo.detach();
  
  Serial.println("STATUS:READY");
  Serial.println("SERVO:90");
  Serial.println("LED:OFF");
  
  digitalWrite(ledPin, LOW);
  delay(1000);
}

void loop() {
  if (Serial.available()) {
    String command = Serial.readStringUntil('\n');
    command.trim();
    processCommand(command);
  }
  
  if (blinking) {
    unsigned long currentMillis = millis();
    if (currentMillis - previousBlinkMillis >= blinkInterval) {
      previousBlinkMillis = currentMillis;
      digitalWrite(ledPin, !digitalRead(ledPin));
    }
  }
  
  if (digitalRead(tslIntPin) == LOW) {
    Serial.println("ALERT: Bright light detected!");
    delay(1000);
  }
  
  static unsigned long previousSensorRead = 0;
  if (millis() - previousSensorRead >= 2000) {
    previousSensorRead = millis();
    readSensors();
  }
}

void processCommand(String command) {
  Serial.print("CMD:");
  Serial.println(command);
  
  if (command == "STATUS") {
    Serial.println("STATUS:READY");
    Serial.println("SERVO:" + String(currentServoAngle));
    Serial.println(blinking ? "LED:BLINK" : (ledState ? "LED:ON" : "LED:OFF"));
  }
  else if (command.startsWith("SERVO:")) {
    int angle = command.substring(6).toInt();
    if (angle >= 0 && angle <= 180 && angle != currentServoAngle) {
      if (!myServo.attached()) {
        myServo.attach(servoPin, 544, 2400);
      }
      currentServoAngle = angle;
      myServo.write(angle);
      delay(500);
      myServo.detach();
      Serial.println("SERVO:" + String(angle));
    }
  }
  else if (command == "LED:ON") {
    digitalWrite(ledPin, HIGH);
    ledState = true;
    blinking = false;
    Serial.println("LED:ON");
  }
  else if (command == "LED:OFF") {
    digitalWrite(ledPin, LOW);
    ledState = false;
    blinking = false;
    Serial.println("LED:OFF");
  }
  else if (command == "LED:BLINK") {
    blinking = true;
    blinkInterval = 500;
    Serial.println("LED:BLINK_STARTED");
  }
  else if (command == "LED:BLINK,SLOW") {
    blinking = true;
    blinkInterval = 1000;
    Serial.println("LED:BLINK_STARTED");
  }
  else if (command == "LED:BLINK,FAST") {
    blinking = true;
    blinkInterval = 200;
    Serial.println("LED:BLINK_STARTED");
  }
  else if (command == "LED:BLINK,PULSE") {
    blinking = true;
    blinkInterval = 600;
    Serial.println("LED:BLINK_STARTED");
  }
  else if (command.startsWith("LED:BLINK,CUSTOM")) {
    blinking = true;
    int comma1 = command.indexOf(',');
    int comma2 = command.indexOf(',', comma1 + 1);
    
    if (comma1 != -1 && comma2 != -1) {
      int onTime = command.substring(comma2 + 1).toInt();
      blinkInterval = onTime;
    }
    
    Serial.println("LED:BLINK_STARTED");
  }
}

void readSensors() {
  float temperature = -999;
  float humidity = -999;
  
  for (int i = 0; i < 3; i++) {
    temperature = dht.readTemperature();
    humidity = dht.readHumidity();
    
    if (!isnan(temperature) && !isnan(humidity)) {
      break;
    }
    delay(100);
  }
  
  long duration;
  float distance = 0;
  
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);
  
  duration = pulseIn(echoPin, HIGH, 30000);
  if (duration > 0) {
    distance = duration * 0.034 / 2;
  }
  
  if (distance <= 0 || distance > 400) {
    distance = 0;
  }
  
  float lux = 0;
  int visible = 0;
  
  if (tsl.begin()) {
    uint32_t lum = tsl.getFullLuminosity();
    uint16_t ir, full;
    ir = lum >> 16;
    full = lum & 0xFFFF;
    
    lux = tsl.calculateLux(full, ir);
    visible = full - ir;
    
    if (isnan(lux) || lux < 0) {
      lux = 0;
    }
    if (visible < 0) {
      visible = 0;
    }
  }
  
  Serial.println("TEMP:" + String(temperature, 1));
  Serial.println("HUM:" + String(humidity, 1));
  Serial.println("LIGHT:" + String(visible));
  Serial.println("LUX:" + String(lux, 1));
  Serial.println("DISTANCE:" + String(distance, 1));
}

